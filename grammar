# Comments can be added anywhere whitespace can be added.


# Expression / argument
<expr> ::= <exprp>
         | <>


# Expression that must be present (cannot be empty).
<exprp> ::= <text> <expr>
          | "{" <expr> "}" <expr>
          | "{" <dictp> "}" <expr>
          | "[" <seq> "]" <expr>
          | <direxpr> <expr>


<dict> ::= <dictp>
         | <>


<dictp> ::= <key> ":" <expr>
          | <key> ":" <expr> ";" <dict>
          | <key> ";" <dict>
          | ":" <dict>


<seq> ::= <exprp>
        | <expr> ";" <seq>
        | ":" <seq>
        | <>


# Directive expression
<direxpr> ::= "<" <key> <attr> ">"<dirarg>
            | "<" <key> ":" <attr> ">" <expr> "</" <key> ">"
            | "<" <key> ":" <attr> ">" <expr> "</" ">"


# Directive attribute
<attr> ::= <>
         | <key> <attr>
         | <key> ":" <key> <attr>
         | <key> ":" "{" <expr> "}" <attr>
         | <key> ":" "{" <dictp> "}" <attr>
         | <key> ":" "[" <seq> "]" <attr>
         | <key> ":" <direxpr> <attr>


# Directive argument
<dirarg> ::= < >
           | ":"<key><dirarg>
           | ":""{" <expr> "}"<dirarg>
           | ":""{" <dictp> "}"<dirarg>
           | ":""[" <seq> "]"<dirarg>
           | ":""<" <key> <attr> ">"<dirarg>
           | ":""<"">"":"<direxpr>


<text> ::= <words>
         | <quote>


# A quote is anything enclosed within the quotes.
<quote> ::= "\"" ... "\""


<words> ::= <word> <words>
          | <>


<key> ::= <word>
        | <quote>


# A word is any sequence of glyphs that contains
# no reserved characters (except if they are escaped.)
<word> ::= ...


# No whitespace allowed
<> ::= ...
